%% ************************************************************************
% Quantitative Dynamic Macroeconomics
% Project: New-Keynesian (NK) Model with Capital and Fiscal Policy
% Konstantin Gantert - Tilburg University
% 01/04/2025
% *************************************************************************
% ------------------------------------------------------------------------------
% I) Define Variables and Parameters
% ------------------------------------------------------------------------------

var
    // Observable Variables
    Y           ${Y}$           (long_name='Real Output')
    L           ${L}$           (long_name='Total Hours')
    K           ${K}$           (long_name='Capital Stock')
    X           ${X}$           (long_name='Capital Investment')
    C           ${C}$           (long_name='Real Consumption')
    w           ${w}$           (long_name='Real Wage')
    r           ${r_K}$         (long_name='Real Capital Interest Rate')
    rb          ${r_B}$         (long_name='Nominal Bond Interest Rate')
    mc          ${mc}$          (long_name='Real Marginal Costs')
    pii         ${\pi}$         (long_name='Price Inflation')
    lg          ${l_g}$         (long_name='Government liabilities')
    m           ${m}$           (long_name='Money supply')
    tauu        ${\tau}$        (long_name='Variable lump-sum taxes')
    
    // Marginal Utility & Penalty Prices
    muc         ${muc}$         (long_name='Marginal Utility out of Consumption')
    muu         ${\mu}$         (long_name='Budget Constraint Penalty Price')
    tet         ${\theta}$      (long_name='Capital Law of Motion Penalty Price')

    // Adjustment Cost Functions
    cp          ${c_P}$         (long_name='Price Adjustment Cost')
    cpp         ${c^\prime_P}$  (long_name='Marginal Price Adjustment Cost')
    ci          ${c_I}$         (long_name='Investment Adjustment Cost')
    cii         ${c^\prime_I}$  (long_name='Marginal Investment Adjustment Cost')
    
    // AR(1) Shock Processes - Exogenous Shocks
    // Default shock
%    sM          ${s_M}$         (long_name='Monetary policy shock process')
    sMC          ${s_MC}$         (long_name='Marginal cost shock process')
    ;

predetermined_variables     
K, muu, lg                      // Here we we set our control variables with time indext "t+1" that are set in "t"!
    ;

varexo          // Here we set our exogenous variables. 
                // Note: AR(1) processes are endogenous to the model, only the shock is exogenous.
//    eM          ${\varepsilon_M}$   (long_name='Monetary policy shock')
    eMC          ${\varepsilon_MC}$   (long_name='marginal cost shock')
    ;   

parameters
    // Deep parameters
    betta       ${\beta}$           (long_name='Period discount factor')
    sig         ${\sigma}$          (long_name='Intertemporal elasticity of substitution')
    gam         ${\gamma}$          (long_name='Inverse Frisch labor supply elasticity')
    vphi        ${\varphi}$         (long_name='Labor disutility level parameter')
    nuu         ${\nu}$             (long_name='Money velocity')
    delt        ${\delta}$          (long_name='Private capital depreciation rate')
    phiX        ${\phi_X}$          (long_name='Investment adjustment cost parameter')
    alp         ${\alpha}$          (long_name='Capital elasticity w.r.t. output')
    rhoo        ${\rho}$            (long_name='Elasticity of substitution between differentiated goods')
    kap         ${\kappa}$          (long_name='Price adjustment cost parameter')

    // Fiscal policy and taxes
    tauL        ${\tau_L}$          (long_name='Labor income tax')
    tauK        ${\tau_K}$          (long_name='Capital income tax')
    tauC        ${\tau_C}$          (long_name='Consumption tax')
    gamLG       ${\gamma_{l_g}}$    (long_name='Fiscal rule coefficient w.r.t. government liabilities')
    lgY_ss      ${lg^Y_{ss}}$       (long_name='Public-debt-to-GDP steady-state target')
    g_ss        ${G_{ss}}$          (long_name='Government spending steady-state target')

    // Monetary policy
    tetPi       ${\theta_{\pi}}$    (long_name='Taylor coefficient w.r.t. inflation')

    // Shock standard deviations
%    stdM        ${\sigma_M}$        (long_name='Standard deviation monetary policy shock')
    stdMC        ${\sigma_MC}$        (long_name='Standard deviation marginal cost shock')

    // Shock autocorrelations
%    rhoM        ${\rho_M}$          (long_name='Autocorrelation monetary policy shock')
    rhoMC        ${\rho_MC}$          (long_name='Autocorrelation marginal cost shock')
    ;
    
% Assigning values from Matlab workspace to the parameters
% ------------------------------------------------------------------------------
% Deep parameters - Households
set_param_value('betta',par.betta);
set_param_value('sig',par.sig);
set_param_value('nuu',par.nuu);
set_param_value('gam',par.gam);
set_param_value('delt',par.delt);
set_param_value('phiX',par.phiX);

% Deep parameters - Firms
set_param_value('alp',par.alp);
set_param_value('rhoo',par.rhoo);
set_param_value('kap',par.kap);

% Fiscal policy and taxes
set_param_value('tauL',par.tauL);
set_param_value('tauK',par.tauK);
set_param_value('tauC',par.tauC);
set_param_value('gamLG',par.gamLG);
set_param_value('lgY_ss',par.lgY_ss);

% Monetary policy
set_param_value('tetPi',par.tetPi);

% Shock standard deviations
% set_param_value('stdM',par.stdM);
set_param_value('stdMC',par.stdMC);

% Shock autocorrelations
% set_param_value('rhoM',par.rhoM);
set_param_value('rhoMC',par.rhoMC);

%-------------------------------------------------------------------------------
% II) Model Block
%-------------------------------------------------------------------------------

model;

%-------------------------------------------------------------------------------
% Sticky price economy
%-------------------------------------------------------------------------------

% Household
% ---------------------------------

[name='Marginal utility out of consumption']
muc     = C^(-sig);

[name='Labor supply']
w       = (1+tauC+tauu)/(1-tauL-tauu) * (vphi*L^gam) / (muu*muc);

[name='Cash-in-Advance Constraint']
muu*(nuu)     = betta/(1+pii(+1))*muc(+1)/muc*(1+tauC+tauu)/(1+tauC+tauu(+1))*((1-muu(+1))+(nuu)*muu(+1));

[name='Euler Equation (Bond Prices)']
muc     = betta* muu(+1)/muu*(1+tauC+tauu)/(1+tauC+tauu(+1)) * (1+rb)/(1+pii(+1)) * muc(+1);

[name='Capital allocation (Tobins Q/Euler equation)']
tet     = betta*muc(+1)/muc*(1+tauC+tauu)/(1+tauC+tauu(+1)) 
            * ( tet(+1)*(1-delt) + muu(+1)*(r(+1)*(1-tauK-tauu)) );

[name='Optimal capital investment']
muu     = tet*(1-ci-cii) + betta*muc(+1)/muc*(1+tauC+tauu)/(1+tauC+tauu(+1))*tet(+1)*X(+1)/X*cii(+1);

% ---------------------------------
% ---------------------------------
% Intermediate good firm
% ---------------------------------
% ---------------------------------

[name='Labor demand']
w       = (1-alp)*(mc)*Y/L;

[name='Capital demand']
r       = alp*(mc)*Y/K;

[name='NK Phillips curve']
cpp/(1-cp) = (1-rhoo/(mc*exp(sMC)))/(1-rhoo)
            + (1+pii(+1))/(1+rb) *Y(+1)/Y*(mc(+1)*exp(sMC(+1)))/(mc*exp(sMC)) * cpp(+1)/(1-cp(+1));

% Constraints
% ---------------------------------

[name='Resource constraint']
Y       = C + X + g_ss;

[name='Production function']
Y       = (1-cp) * (L)^(1-alp)*K^alp;

[name='Capital law of motion']
K(+1)   = (1-delt)*K + (1-ci)*X;

[name='Cash-in-advance constraint']
m       = (nuu)*(1+tauC+tauu)*C;

[name='Government budget constraint']
lg(+1)  = (1+rb)/(1+pii(+1))*lg  - rb*m
            + (1+rb)*( g_ss-(tauC+tauu)*C-(tauL+tauu)*w*L-(tauK+tauu)*r*K );

% Adjustment cost functions
% ---------------------------------

[name='Price adjustment cost']
cp      = kap/2*pii^2;

[name='Marginal price adjustment cost']
cpp     = kap*pii*(1+pii);

[name='Investment adjustment cost']
ci      = phiX/2*(X/X(-1)-1)^2;

[name='Marginal investment adjustment cost']
cii     = phiX*(X/X(-1)-1)*X/X(-1);

% Policy rules
% ---------------------------------

% [name='Monetary policy rule']
(1+rb)/(1+steady_state(rb)) = ((1+pii)/(1+steady_state(pii)))^(tetPi) ; % DELETED * exp(sM) to m here!!!

[name='Fiscal policy rule']
tauu - steady_state(tauu) 
        = gamLG * ( lg - steady_state(lg) );

%-------------------------------------------------------------------------------
% Exogenous processes
%-------------------------------------------------------------------------------

% [name='Monetary policy shock process']
% sM      = rhoM*sM(-1) + eM;
[name='Marginal cost shock process']
sMC      = rhoMC*sMC(-1) + eMC;

end;

%-------------------------------------------------------------------------------
% III) Steady-State Calculations
%-------------------------------------------------------------------------------

//resid;
//check;

%--------------------------------------------------------------------------
% IV) Shock processes
%--------------------------------------------------------------------------

shocks;
%    var eM  = stdM^2;
    var eMC  = stdMC^2;
end;

%--------------------------------------------------------------------------
% VI) Simulation
%--------------------------------------------------------------------------

stoch_simul(order=@{approx_order},
            // nodisplay,
            // nograph,
            noprint,
            pruning,
            irf=@{irf_periods},
            periods=@{sim_periods},
            replic=@{replic_number},
            simul_replic=@{simul_replic_number},
            drop=@{drop_number}
            );

send_endogenous_variables_to_workspace ;
send_exogenous_variables_to_workspace ;
send_irfs_to_workspace ;

%----------------------------- End of File --------------------------------